---
title: "Survival"
author: "UNC EPID 722: Dr. Steve Cole"
date: "February 9, 2016"
output:
  html_document:
   toc: true
   toc_depth: 4 
   theme: united
   number_sections: true
---

***NOTE: ALL SAS code below copied from 2016 EPID 722 lecture material. SAS code based on Steve Cole's programs titled, "program6.25jan16.sas". R code below is an  adaptation to the SAS code written by Ann Von Holle.***

```{r setup, echo=FALSE}
# see http://stackoverflow.com/questions/24585254/working-with-knitr-using-subdirectories
  library(knitr)
  opts_knit$set(root.dir=normalizePath('../'))
  #opts_chunk$set(fig.path = "../figures/", dev='pdf') # corrected path and added dev
  opts_chunk$set(fig.width=12, fig.height=8, fig.align="left", echo=T, warning=FALSE, message=FALSE)
```

### Preliminaries

#### Specify packages for R

```{r}
  #install.packages(c("knitr", "foreign", "tableone", "MCMCpack")) # Note: you only need to do this once. then only if you want updates.
#  install.packages("data.table", "rms") # watch out, rms loads a lot of other packages
  library(data.table)
  library(reshape2)
  library(survival)
  library(ggplot2)
  library(rms)
  library(muhaz)
  set.seed(123) # set seed so you get same results each time you run.
```

```{r, echo=FALSE}
saspath <- 'C:/Program Files/SASHome/SASFoundation/9.4/sas.exe'
sasopts <- "-nosplash -log 'c:\\temp'  -ls 80 -ps 60  -nocenter -nodate" # see http://bit.ly/1QB4ZTb
```

### Read file and add in censor at 1/2 year to ease exposition

#### SAS

<!--Note: Use permanent data file b from SAS code in mle.Rmd file.-->
```{r s-read, engine='sas', engine.path=saspath, engine.opts=sasopts, results='markup', echo=TRUE, message=F, warning=FALSE, eval=T}
libname mle "c:\temp";
%let dir = c:\temp;

* Read ASCII file;
data mle.b;
	infile "&dir\hividu15dec15.dat"; 
	input id 1-4 idu 6 white 8 age 10-11 cd4 13-16 drop 18 delta 20 @22 art 6.3 @29 t 6.3;
	if t>.5 then do; t=.5; delta=0; end;
run;
```

#### R

Read the data (created in ../L06-mle/mle.Rmd).
```{r read}
#getwd() # get the working directory
b = read.csv("c:/temp/hividu15dec15.csv", header=T) # read in data.
summary(b$delta)

b = within(b, {
  delta = ifelse(t>0.5, 0, delta)
  t = ifelse(t>0.5, 0.5, t)
})
```

### Time from 12/6/95 to AIDS or death in WIHS

#### SAS

<!--Note: Use permanent data file b from SAS code in mle.Rmd file.-->
```{r s-part1, engine='sas', engine.path=saspath, engine.opts=sasopts, results='markup', echo=TRUE, message=F, warning=FALSE, eval=T}
libname mle "c:\temp";
%let dir = c:\temp;
data a; set mle.b; run;

*Data;
proc means data=a n mean sum min max; 
	var delta t;
	title "Time from 12/6/95 to AIDS or death in WIHS";
run;

```

#### R

```{r}
summary(b[,c("delta", "t")])
```

### Product limit survival curve

#### SAS

<!--Note: Use permanent data file b from SAS code in mle.Rmd file.-->
```{r s-part2, engine='sas', engine.path=saspath, engine.opts=sasopts, results='markup', echo=TRUE, message=F, warning=FALSE, eval=T}
libname mle "c:\temp";
%let dir = c:\temp;
data a; set mle.b; run;


*Product limit survival curve data;
proc phreg data=a noprint;
	model t*delta(0)=;
	baseline out=b survival=s stderr=se/method=pl; *output survival function and se;
	output out=c(keep=t n) atrisk=n/method=pl; *output numbers in risk sets;
proc sort data=c nodups; by t; *order times;
data c; set c; by t; if first.t; *keep distinct times;
data d; merge b c; by t; if t=0 then n=1164; if s>.; *merge keeping event times;
data mle.d;
	set d;
	r=1-s;
	se2=s*sqrt((1-s)/n); *Peto se;
	if r>0 then do;
		lo=max(0,r-1.96*se);
		hi=min(1,r+1.96*se);
	end;
	label s="Survival" r="Risk" t="Years";
run;

proc print data=mle.d noobs;
	var t n r s se se2;
run;
	
*Plot;
*ods listing gpath="Y:\\Cole\\Teaching\\EPID722\2016\\";
ods listing gpath="c:\\temp";
ods graphics/reset imagename="Survival" imagefmt=jpeg height=8in width=8in;
proc sgplot data=mle.d noautolegend;
	yaxis values=(0 to .1 by .02);
	xaxis values=(0 to .5 by .1);
	step x=t y=r/lineattrs=(color=black);
	step x=t y=lo/lineattrs=(color=black pattern=dot);
	step x=t y=hi/lineattrs=(color=black pattern=dot);
	run; quit;

```

![](c:\temp\Survival.jpeg) <!-- Add in the Survival.jpeg from SAS program above...-->

#### R

```{r r-part2}
s.1 = survfit(Surv(t, delta) ~ 1, data=b)

summary(s.1)

plot(s.1, lwd=1.5, 
     fun = function(y) 1-y ,
     xlab="Time (years)", 
     ylab="Risk of outcome",
     ylim=c(0,0.1),
     xlim=c(0,0.5),
     main="Time from 12/6/95 to AIDS or death in WIHS",
     cex.lab=1, cex.axis=1, cex.main=1, cex.sub=1.5, 
     mark.time=F)
```

### Hazard

#### SAS

<!--Note: Use permanent data file b from SAS code in mle.Rmd file.-->
```{r s-part3, engine='sas', engine.path=saspath, engine.opts=sasopts, results='markup', echo=TRUE, message=F, warning=FALSE, eval=T}
libname mle "c:\temp";
%let dir = c:\temp;
data d; set mle.d; run;

*Some other pieces;
proc sort data=d; 
	by descending s;
data d; 
	set d; 
	by descending s;
	retain chm1 0 sm1 1 tm1 0;
	if t=0 then do; d=0; *n=315; end;
	deltat=t-tm1; 
	if s>0 then ch=-log(s);
	y=round((ch-chm1)*n,1);	
	*above recreates y because SAS does not output;
	if deltat>0 then h=y/(n*deltat);
	label n=;
	logh=log(h);
	output;
	chm1=ch; sm1=s; tm1=t;
	drop chm1 sm1 tm1;
proc print data=d noobs; 
	title2 "Hazard function";
	var t y n deltat h;

*ods listing gpath="Y:\\Cole\\Teaching\\EPID722\\2016\\";
ods listing gpath="c:\\temp";
ods graphics/reset imagename="Hazard" imagefmt=jpeg height=8in width=8in;
proc sgplot data=d noautolegend;
	xaxis values=(0 to .5 by .1);
	loess x=t y=h/smooth=.6;
	pbspline x=t y=h/lineattrs=(color=red) nomarkers;

data d; 
	set d;
	by descending s;
	retain v 0;
	v=v+y/(n*(n-y));
	se3=sqrt(s**2*v); *se by hand;
	ch=-log(s);
	output;
proc print data=d noobs; 
	title2 "Standard error by hand";
	var t ch s se se3;
run;
```

![](c:\temp\Hazard.jpeg) <!-- Add in the Hazard.jpeg from SAS program above...-->

#### R

```{r r-part3}
fmuhaz = muhaz(b$t, b$delta,
              max.time=0.5,
              bw.method="g")
plot(fmuhaz, xlab="Years", ylab="h")
# see arxiv.org/pdf/1509.03253
```
